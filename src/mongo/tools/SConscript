# -*- mode: python; -*-

Import('env')
Import('get_option')

env = env.Clone()

yamlEnv = env.Clone()
yamlEnv.InjectThirdParty(libraries=['yaml'])

mongobridge = env.Program(
    target="mongobridge",
    source=[
        "bridge.cpp",
        "bridge_commands.cpp",
        "mongobridge_options.cpp",
        env.Idlc("mongobridge_options.idl")[0],
        "mongobridge_options_init.cpp"
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/db/dbmessage',
        '$VARIANT_DIR/mongo/rpc/rpc',
        '$VARIANT_DIR/mongo/transport/message_compressor',
        '$VARIANT_DIR/mongo/transport/message_compressor_options_server',
        '$VARIANT_DIR/mongo/transport/service_entry_point',
        '$VARIANT_DIR/mongo/transport/service_executor',
        '$VARIANT_DIR/mongo/transport/transport_layer',
        '$VARIANT_DIR/mongo/util/net/network',
        '$VARIANT_DIR/mongo/util/options_parser/options_parser_init',
        '$VARIANT_DIR/mongo/util/signal_handlers',
    ],
    AIB_COMPONENT='tools',
)

mongoebench = yamlEnv.Program(
    target='mongoebench',
    source=[
        'mongoebench_main.cpp',
        'mongoebench_options.cpp',
        'mongoebench_options_init.cpp',
        env.Idlc('mongoebench_options.idl')[0],
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/db/dbdirectclient',
        '$VARIANT_DIR/mongo/db/storage/storage_options',
        '$VARIANT_DIR/mongo/db/storage/wiredtiger/storage_wiredtiger' if get_option('wiredtiger') == 'on' else [],
        '$VARIANT_DIR/mongo/embedded/embedded',
        '$VARIANT_DIR/mongo/embedded/embedded_integration_helpers',
        '$VARIANT_DIR/mongo/shell/benchrun',
        '$VARIANT_DIR/mongo/util/signal_handlers',
    ],
    AIB_COMPONENT="embedded-test",
    AIB_COMPONENTS_EXTRA=[
        "tests",
        "benchmarks",
    ],
)

hygienic = get_option('install-mode') == 'hygienic'
if not hygienic:
    env.Install("#/", mongobridge)
    env.Install("#/", mongoebench)

env.Alias('all', mongoebench)  # This ensures it compiles and links, but doesn't copy it anywhere.
