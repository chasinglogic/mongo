# -*- mode: python -*-

Import("env")
Import("has_option")

env = env.Clone()

env.Library(
    target="test_commands_enabled",
    source=[
        "test_commands_enabled.cpp",
        env.Idlc('test_commands_enabled.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/idl/server_parameter',
        "server_status_core",
    ]
)

env.Library(
    target='server_status_core',
    source=[
        'server_status_internal.cpp',
        'server_status_metric.cpp',
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
    ]
)

env.Library(
    target='server_status',
    source=[
        'server_status.cpp',
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/db/commands',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/db/auth/auth',
        '$VARIANT_DIR/mongo/db/auth/authprivilege',
        '$VARIANT_DIR/mongo/db/server_options_core',
        '$VARIANT_DIR/mongo/db/stats/counters',
        '$VARIANT_DIR/mongo/util/processinfo',
        '$VARIANT_DIR/mongo/util/net/http_client',
        'server_status_core',
    ],
)

env.Library(
    target='server_status_servers',
    source=[
        'server_status_servers.cpp',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/db/stats/counters',
        '$VARIANT_DIR/mongo/transport/message_compressor',
        '$VARIANT_DIR/mongo/transport/service_executor',
        '$VARIANT_DIR/mongo/util/net/ssl_manager',
        'server_status',
        'server_status_core',
    ],
)

env.Library(
    target="write_commands_common",
    source=[
        "write_commands/write_commands_common.cpp",
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/db/auth/auth',
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/db/auth/authprivilege',
        '$VARIANT_DIR/mongo/db/commands',
        '$VARIANT_DIR/mongo/db/ops/write_ops_parsers',
    ],
)

env.Library(
    target="feature_compatibility_parsers",
    source=[
        "feature_compatibility_version_parser.cpp",
        "feature_compatibility_version_command_parser.cpp",
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/db/command_generic_argument',
        '$VARIANT_DIR/mongo/db/namespace_string',
    ],
)

# Commands available in every process that executes commands
env.Library(
    target='core',
    source=[
        'end_sessions_command.cpp',
        'fail_point_cmd.cpp',
        'find_and_modify_common.cpp',
        'generic.cpp',
        'hashcmd.cpp',
        'kill_all_sessions_by_pattern_command.cpp',
        'kill_all_sessions_command.cpp',
        'kill_sessions_command.cpp',
        'parameters.cpp',
        'refresh_logical_session_cache_now.cpp',
        'refresh_sessions_command.cpp',
        'rename_collection_common.cpp',
        'start_session_command.cpp',
        env.Idlc('parameters.idl')[0],
        env.Idlc('sessions_commands.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/bson/mutable/mutable_bson',
        '$VARIANT_DIR/mongo/db/auth/auth',
        '$VARIANT_DIR/mongo/db/auth/authprivilege',
        '$VARIANT_DIR/mongo/db/commands',
        '$VARIANT_DIR/mongo/db/common',
        '$VARIANT_DIR/mongo/db/kill_sessions',
        '$VARIANT_DIR/mongo/db/logical_session_cache_impl',
        '$VARIANT_DIR/mongo/db/logical_session_cache',
        '$VARIANT_DIR/mongo/db/logical_session_id_helpers',
        '$VARIANT_DIR/mongo/db/logical_session_id',
        '$VARIANT_DIR/mongo/db/mongohasher',
        '$VARIANT_DIR/mongo/db/server_options_core',
        '$VARIANT_DIR/mongo/idl/server_parameter',
        '$VARIANT_DIR/mongo/logger/parse_log_component_settings',
        '$VARIANT_DIR/mongo/rpc/protocol',
        'test_commands_enabled',
    ],
)

# Commands available in all mongodb server processes (mongod, mongos, etc.)
env.Library(
    target='servers',
    source=[
        'conn_pool_stats.cpp',
        'conn_pool_sync.cpp',
        'connection_status.cpp',
        'drop_connections_command.cpp',
        'generic_servers.cpp',
        'isself.cpp',
        'logical_session_server_status_section.cpp',
        'mr_common.cpp',
        'reap_logical_session_cache_now.cpp',
        'traffic_recording_cmds.cpp',
        'user_management_commands_common.cpp',
        env.Idlc('drop_connections.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/client/clientdriver_minimal',
        '$VARIANT_DIR/mongo/db/commands',
        '$VARIANT_DIR/mongo/db/common',
        '$VARIANT_DIR/mongo/db/log_process_details',
        '$VARIANT_DIR/mongo/db/logical_session_cache_impl',
        '$VARIANT_DIR/mongo/db/logical_session_cache',
        '$VARIANT_DIR/mongo/db/logical_session_id_helpers',
        '$VARIANT_DIR/mongo/db/logical_session_id',
        '$VARIANT_DIR/mongo/db/repl/isself',
        '$VARIANT_DIR/mongo/db/repl/repl_coordinator_interface',
        '$VARIANT_DIR/mongo/db/session_catalog',
        '$VARIANT_DIR/mongo/db/traffic_recorder',
        '$VARIANT_DIR/mongo/executor/egress_tag_closer_manager',
        '$VARIANT_DIR/mongo/executor/task_executor_pool',
        '$VARIANT_DIR/mongo/rpc/client_metadata',
        '$VARIANT_DIR/mongo/s/coreshard',
        '$VARIANT_DIR/mongo/s/sharding_legacy_api',
        '$VARIANT_DIR/mongo/scripting/scripting_common',
        '$VARIANT_DIR/mongo/util/ntservice',
        'authentication_commands',
        'core',
        'feature_compatibility_parsers',
        'server_status',
        'test_commands_enabled',
    ]
)

env.Library(
    target="authentication_commands",
    source=[
        'authentication_commands.cpp',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/db/audit',
        '$VARIANT_DIR/mongo/db/auth/sasl_options',
        '$VARIANT_DIR/mongo/db/auth/user_document_parser',
        '$VARIANT_DIR/mongo/db/commands',
        '$VARIANT_DIR/mongo/util/net/ssl_manager',
    ]
)

env.Library(
    target="mongod_fsync",
    source=[
        "fsync.cpp",
    ],
    LIBDEPS_PRIVATE=[
	'$VARIANT_DIR/mongo/db/auth/authprivilege',
        '$VARIANT_DIR/mongo/db/commands',
        '$VARIANT_DIR/mongo/db/concurrency/write_conflict_exception',
        '$VARIANT_DIR/mongo/db/curop',
        '$VARIANT_DIR/mongo/db/storage/backup_cursor_hooks',
        'fsync_locked',
    ]
)

env.Library(
    target="mongod_fcv",
    source=[
        "feature_compatibility_version.cpp",
        env.Idlc('feature_compatibility_version.idl')[0],
    ],
    LIBDEPS=[
        'feature_compatibility_parsers',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/db/commands',
        '$VARIANT_DIR/mongo/db/dbdirectclient',
        '$VARIANT_DIR/mongo/db/kill_sessions_local',
        '$VARIANT_DIR/mongo/idl/server_parameter',
        '$VARIANT_DIR/mongo/executor/egress_tag_closer_manager',
    ],
)

env.Library(
    target='fsync_locked',
    source=[
        'fsync_locked.cpp',
    ],
    LIBDEPS=[
    ],
)

env.Library(
    target='list_databases_command',
    source=[
        env.Idlc('list_databases.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/idl/idl_parser',
    ],
)

# Commands that are present in both mongod and embedded
env.Library(
    target="standalone",
    source=[
        "count_cmd.cpp",
        "create_indexes.cpp",
        "current_op.cpp",
        "dbcommands.cpp",
        "distinct.cpp",
        "drop_indexes.cpp",
        "explain_cmd.cpp",
        "find_and_modify.cpp",
        "find_cmd.cpp",
        "get_last_error.cpp",
        "getmore_cmd.cpp",
        "http_client.cpp",
        env.Idlc('http_client.idl')[0],
        "index_filter_commands.cpp",
        "kill_op.cpp",
        "killcursors_cmd.cpp",
        "lock_info.cpp",
        "list_collections.cpp",
        "list_databases.cpp",
        "list_indexes.cpp",
        "pipeline_command.cpp",
        "plan_cache_commands.cpp",
        "rename_collection_cmd.cpp",
        "repair_cursor.cpp",
        "run_aggregate.cpp",
        "sleep_command.cpp",
        "validate.cpp",
        "write_commands/write_commands.cpp",
        env.Idlc('create.idl')[0],
        env.Idlc('enable_coordinator_for_create_indexes_command.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/db/catalog/catalog_helpers',
        '$VARIANT_DIR/mongo/db/catalog/collection_catalog_helper',
        '$VARIANT_DIR/mongo/db/catalog/database_holder',
        '$VARIANT_DIR/mongo/db/catalog/index_key_validate',
        '$VARIANT_DIR/mongo/db/catalog/multi_index_block',
        '$VARIANT_DIR/mongo/db/command_can_run_here',
        '$VARIANT_DIR/mongo/db/commands',
        '$VARIANT_DIR/mongo/db/curop_failpoint_helpers',
        '$VARIANT_DIR/mongo/db/index_builds_coordinator_interface',
        '$VARIANT_DIR/mongo/db/ops/write_ops_exec',
        '$VARIANT_DIR/mongo/db/pipeline/mongo_process_interface',
        '$VARIANT_DIR/mongo/db/query_exec',
        '$VARIANT_DIR/mongo/db/query/command_request_response',
        '$VARIANT_DIR/mongo/db/repl/replica_set_messages',
        '$VARIANT_DIR/mongo/db/rw_concern_d',
        '$VARIANT_DIR/mongo/db/stats/counters',
        '$VARIANT_DIR/mongo/db/stats/server_read_concern_write_concern_metrics',
        '$VARIANT_DIR/mongo/db/storage/storage_engine_common',
        '$VARIANT_DIR/mongo/db/transaction',
        '$VARIANT_DIR/mongo/db/views/views_mongod',
        '$VARIANT_DIR/mongo/util/net/http_client',
        'core',
        'current_op_common',
        'fsync_locked',
        'kill_common',
        'list_collections_filter',
        'list_databases_command',
        'test_commands_enabled',
        'write_commands_common',
    ],
)

# Commands required by the shell to connect
env.Library(
    target="shell_protocol",
    source=[
        "test_commands.cpp",
        "whats_my_uri_cmd.cpp",
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/db/catalog/catalog_helpers',
        '$VARIANT_DIR/mongo/db/catalog/collection',
        '$VARIANT_DIR/mongo/db/commands',
        'test_commands_enabled',
    ],
)

env.Library(
    target='set_index_commit_quorum_idl',
    source=[
        env.Idlc('set_index_commit_quorum.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/db/catalog/commit_quorum_options',
        '$VARIANT_DIR/mongo/db/rw_concern_d',
        '$VARIANT_DIR/mongo/idl/idl_parser',
    ],
)

# Commands that should only be present in mongod
env.Library(
    target="mongod",
    source=[
        "apply_ops_cmd.cpp",
        "collection_to_capped.cpp",
        "compact.cpp",
        "cpuload.cpp",
        "dbcheck.cpp",
        "dbcommands_d.cpp",
        "dbhash.cpp",
        "driverHelpers.cpp",
        "haystack.cpp",
        "map_reduce_command.cpp",
        "map_reduce_finish_command.cpp",
        "mr.cpp",
        "oplog_application_checks.cpp",
        "oplog_note.cpp",
        "resize_oplog.cpp",
        "restart_catalog_command.cpp",
        "set_feature_compatibility_version_command.cpp",
        "set_index_commit_quorum_command.cpp",
        "shutdown_d.cpp",
        "snapshot_management.cpp",
        "top_command.cpp",
        "touch.cpp",
        "txn_cmds.cpp",
        "user_management_commands.cpp",
        "vote_commit_index_build_command.cpp",
        env.Idlc('vote_commit_index_build.idl')[0],
    ],
    LIBDEPS=[
        'txn_cmd_request',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/client/clientdriver_minimal',
        '$VARIANT_DIR/mongo/db/auth/auth',
        '$VARIANT_DIR/mongo/db/auth/authprivilege',
        '$VARIANT_DIR/mongo/db/auth/role_graph',
        '$VARIANT_DIR/mongo/db/auth/sasl_options',
        '$VARIANT_DIR/mongo/db/auth/user_document_parser',
        '$VARIANT_DIR/mongo/db/auth/user',
        '$VARIANT_DIR/mongo/db/background',
        '$VARIANT_DIR/mongo/db/catalog/catalog_control',
        '$VARIANT_DIR/mongo/db/catalog/catalog_helpers',
        '$VARIANT_DIR/mongo/db/catalog/collection_catalog_helper',
        '$VARIANT_DIR/mongo/db/catalog/index_key_validate',
        '$VARIANT_DIR/mongo/db/cloner',
        '$VARIANT_DIR/mongo/db/commands',
        '$VARIANT_DIR/mongo/db/curop_failpoint_helpers',
        '$VARIANT_DIR/mongo/db/dbhelpers',
        '$VARIANT_DIR/mongo/db/exec/stagedebug_cmd',
        '$VARIANT_DIR/mongo/db/index_builds_coordinator_interface',
        '$VARIANT_DIR/mongo/db/repl/dbcheck',
        '$VARIANT_DIR/mongo/db/repl/oplog',
        '$VARIANT_DIR/mongo/db/repl/repl_coordinator_interface',
        '$VARIANT_DIR/mongo/db/rw_concern_d',
        '$VARIANT_DIR/mongo/db/s/sharding_runtime_d',
        '$VARIANT_DIR/mongo/idl/idl_parser',
        '$VARIANT_DIR/mongo/s/sharding_legacy_api',
        '$VARIANT_DIR/mongo/util/net/ssl_manager',
        'core',
        'kill_common',
        'mongod_fcv',
        'mongod_fsync',
        'profile_common',
        'servers',
        'set_index_commit_quorum_idl',
        'shell_protocol',
        'standalone',
        'test_commands_enabled',
    ],
)

env.Library(
    target='kill_common',
    source=[
        'killcursors_common.cpp',
        'kill_op_cmd_base.cpp'
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/db/auth/authprivilege',
        '$VARIANT_DIR/mongo/db/audit',
        '$VARIANT_DIR/mongo/db/commands',
        '$VARIANT_DIR/mongo/db/auth/authorization_manager_global',
        '$VARIANT_DIR/mongo/db/query/command_request_response',
    ],
)

env.Library(
    target='current_op_common',
    source=[
        'current_op_common.cpp'
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/db/commands',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/db/namespace_string',
        '$VARIANT_DIR/mongo/db/pipeline/aggregation_request',
        '$VARIANT_DIR/mongo/db/query/command_request_response',
        '$VARIANT_DIR/mongo/db/service_context',
        'test_commands_enabled'
    ],
)

env.Library(
    target='profile_common',
    source=[
        'profile_common.cpp',
        env.Idlc('profile.idl')[0],
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/db/commands',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/db/auth/auth',
        '$VARIANT_DIR/mongo/db/auth/authprivilege',
        '$VARIANT_DIR/mongo/db/server_options_core',
    ],
)

env.Library(
    target="list_collections_filter",
    source=[
        'list_collections_filter.cpp',
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
    ],
)

env.Library(
    target='txn_cmd_request',
    source=[
        env.Idlc("txn_cmds.idl")[0],
        env.Idlc("txn_two_phase_commit_cmds.idl")[0],
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/idl/idl_parser',
    ]
)

if has_option('use-cpu-profiler'):
    profEnv = env.Clone()
    profEnv.InjectThirdParty('gperftools')
    profEnv.Library(
        target='cpuprofiler',
        source=[
            'cpuprofile.cpp',
        ],
        LIBDEPS=[
            '$VARIANT_DIR/mongo/db/commands',
            '$VARIANT_DIR/mongo/db/db_raii',
        ],
        PROGDEPS_DEPENDENTS=[
            '$VARIANT_DIR/mongo/mongod',
        ],
    )

env.CppUnitTest(
    target="db_commands_test",
    source=[
        "index_filter_commands_test.cpp",
        "list_collections_filter_test.cpp",
        "mr_test.cpp",
        "plan_cache_commands_test.cpp",
    ],
    LIBDEPS=[
        "$VARIANT_DIR/mongo/db/auth/authmocks",
        "$VARIANT_DIR/mongo/db/commands/list_collections_filter",
        "$VARIANT_DIR/mongo/db/query/query_planner",
        "$VARIANT_DIR/mongo/db/query/query_test_service_context",
        "$VARIANT_DIR/mongo/db/repl/drop_pending_collection_reaper",
        "$VARIANT_DIR/mongo/db/repl/replmocks",
        "$VARIANT_DIR/mongo/db/repl/storage_interface_impl",
        "$VARIANT_DIR/mongo/db/service_context_d",
        "$VARIANT_DIR/mongo/db/service_context_d_test_fixture",
        "mongod",
        "servers",
        "standalone",
    ],
)
