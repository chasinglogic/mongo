# -*- mode: python -*-
Import("env")
Import("free_monitoring")

env = env.Clone()

fmEnv = env.Clone()
fmEnv.InjectThirdParty(libraries=['snappy'])

fmEnv.Library(
    target='free_mon',
    source=[
        'free_mon_processor.cpp',
        'free_mon_queue.cpp',
        'free_mon_op_observer.cpp',
        'free_mon_storage.cpp',
        'free_mon_controller.cpp',
        env.Idlc('free_mon_protocol.idl')[0],
        env.Idlc('free_mon_commands.idl')[0],
        env.Idlc('free_mon_storage.idl')[0],
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/db/concurrency/lock_manager',
        '$VARIANT_DIR/mongo/db/dbhelpers',
        '$VARIANT_DIR/mongo/db/ftdc/ftdc',
        '$VARIANT_DIR/mongo/idl/idl_parser',
        '$VARIANT_DIR/third_party/shim_snappy',
    ],
)

if free_monitoring == "on":
    fmEnv.Library(
        target='free_mon_mongod',
        source=[
            'free_mon_commands.cpp',
            'free_mon_mongod.cpp',
            env.Idlc('free_mon_mongod.idl')[0],
            'free_mon_options.cpp',
            env.Idlc('free_mon_options.idl')[0],
            'free_mon_status.cpp',
        ],
        LIBDEPS=[
            'free_mon',
            '$VARIANT_DIR/mongo/db/commands/server_status',
            '$VARIANT_DIR/mongo/db/ftdc/ftdc_server',
            '$VARIANT_DIR/mongo/util/options_parser/options_parser',
        ],
        LIBDEPS_PRIVATE=[
            '$VARIANT_DIR/mongo/client/clientdriver_network',
            '$VARIANT_DIR/mongo/util/concurrency/thread_pool',
            '$VARIANT_DIR/mongo/util/net/http_client',
        ],
    )
else:
    fmEnv.Library(
        target='free_mon_mongod',
        source=[
            'free_mon_commands_stub.cpp',
            'free_mon_stub.cpp',
        ],
        LIBDEPS_PRIVATE=[
            'free_mon',
            '$VARIANT_DIR/mongo/base',
            '$VARIANT_DIR/mongo/db/commands',
            '$VARIANT_DIR/mongo/db/auth/auth',
            '$VARIANT_DIR/mongo/db/auth/authprivilege',
        ],
    )


fmEnv.CppUnitTest(
    target='db_free_mon_test',
    source=[
        'free_mon_controller_test.cpp',
        'free_mon_queue_test.cpp',
        'free_mon_storage_test.cpp',
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/db/auth/authmocks',
        '$VARIANT_DIR/mongo/db/repl/replmocks',
        '$VARIANT_DIR/mongo/db/repl/storage_interface_impl',
        '$VARIANT_DIR/mongo/db/service_context_d_test_fixture',
        '$VARIANT_DIR/mongo/db/service_context_d',
        '$VARIANT_DIR/mongo/executor/thread_pool_task_executor_test_fixture',
        '$VARIANT_DIR/mongo/util/clock_source_mock',
        'free_mon',
    ],
)
