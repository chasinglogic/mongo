# -*- mode: python -*-

Import("env")

env = env.Clone()

env.Library(
        target='index_descriptor',
        source=[
            'index_descriptor.cpp',
        ],
        LIBDEPS=[
            '$VARIANT_DIR/mongo/base',
            '$VARIANT_DIR/mongo/db/catalog/collection',
            '$VARIANT_DIR/mongo/db/catalog/index_catalog',
        ],
        LIBDEPS_PRIVATE=[
            '$VARIANT_DIR/mongo/db/index_names',
            '$VARIANT_DIR/mongo/db/namespace_string',
        ],
)

env.Library(
        target='duplicate_key_tracker',
        source=[
            'duplicate_key_tracker.cpp',
        ],
        LIBDEPS=[
            '$VARIANT_DIR/mongo/db/service_context',
        ],
        LIBDEPS_PRIVATE=[
            '$VARIANT_DIR/mongo/base',
            '$VARIANT_DIR/mongo/db/curop',
            '$VARIANT_DIR/mongo/db/storage/index_entry_comparison',
        ],
)

env.Library(
        target='key_generator',
        source=[
            'btree_key_generator.cpp',
            'expression_keys_private.cpp',
            'sort_key_generator.cpp',
            'wildcard_key_generator.cpp',
        ],
        LIBDEPS=[
            '$VARIANT_DIR/mongo/base',
            '$VARIANT_DIR/mongo/db/bson/dotted_path_support',
            '$VARIANT_DIR/mongo/db/fts/base_fts',
            '$VARIANT_DIR/mongo/db/geo/geoparser',
            '$VARIANT_DIR/mongo/db/index_names',
            '$VARIANT_DIR/mongo/db/mongohasher',
            '$VARIANT_DIR/mongo/db/projection_exec_agg',
            '$VARIANT_DIR/mongo/db/query/collation/collator_interface',
            '$VARIANT_DIR/third_party/s2/s2',
            'expression_params',
            'index_descriptor',
        ],
)

env.Library(
        target='expression_params',
        source=[
            'expression_params.cpp',
            's2_common.cpp'
        ],
        LIBDEPS=[
            '$VARIANT_DIR/mongo/base',
            '$VARIANT_DIR/mongo/bson/util/bson_extract',
            '$VARIANT_DIR/mongo/db/geo/geometry',
            '$VARIANT_DIR/mongo/db/geo/geoparser',
            '$VARIANT_DIR/mongo/db/mongohasher',
            '$VARIANT_DIR/mongo/db/query/collation/collator_interface',
            '$VARIANT_DIR/third_party/s2/s2',
        ]
)

env.Library(
    target='index_access_method_factory',
        source=[
            'index_access_method_factory.cpp',
        ],
        LIBDEPS=[
            '$VARIANT_DIR/mongo/base',
        ],
)

serveronlyEnv = env.Clone()
serveronlyEnv.InjectThirdParty(libraries=['snappy'])
serveronlyEnv.Library(
    target="index_access_method",
    source=[
        "index_access_method.cpp"
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/db/catalog/index_catalog_entry',
        '$VARIANT_DIR/mongo/db/curop',
        '$VARIANT_DIR/mongo/db/concurrency/write_conflict_exception',
        '$VARIANT_DIR/mongo/db/repl/repl_coordinator_interface',
        '$VARIANT_DIR/mongo/db/storage/encryption_hooks',
        '$VARIANT_DIR/mongo/db/storage/index_entry_comparison',
        '$VARIANT_DIR/mongo/db/storage/storage_options',
        '$VARIANT_DIR/third_party/shim_snappy',
        'index_descriptor',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/db/logical_clock',
        '$VARIANT_DIR/mongo/idl/server_parameter',
    ],
)

env.Library(
    target="index_access_methods",
    source=[
        "2d_access_method.cpp",
        "btree_access_method.cpp",
        "fts_access_method.cpp",
        "hash_access_method.cpp",
        "haystack_access_method.cpp",
        "index_access_method_factory_impl.cpp",
        "s2_access_method.cpp",
        "wildcard_access_method.cpp",
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/db/query_exec',
        'index_access_method',
        'key_generator',
    ],
)

env.Library(
    target="index_build_interceptor",
    source=[
        "index_build_interceptor.cpp",
        env.Idlc('index_build_interceptor.idl')[0],
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
        'duplicate_key_tracker',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/db/catalog/index_timestamp_helper',
        '$VARIANT_DIR/mongo/db/multi_key_path_tracker',
        'index_access_methods',
    ],
)

env.CppUnitTest(
    target='db_index_test',
    source=[
        '2d_key_generator_test.cpp',
        'btree_key_generator_test.cpp',
        'hash_key_generator_test.cpp',
        's2_key_generator_test.cpp',
        'sort_key_generator_test.cpp',
        'wildcard_key_generator_test.cpp',
    ],
    LIBDEPS=[
        'key_generator',
        "$VARIANT_DIR/mongo/db/matcher/expressions",
        '$VARIANT_DIR/mongo/db/mongohasher',
        '$VARIANT_DIR/mongo/db/query/collation/collator_interface_mock',
    ],
)
