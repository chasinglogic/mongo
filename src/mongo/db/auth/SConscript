# -*- mode: python -*-

Import("env")

env = env.Clone()

generateActionTypes = env.Command(
    target=[
        'action_type.h', 'action_type.cpp'
    ],
    source=[
        'generate_action_types.py',
        'action_types.txt'
    ],
    action='$PYTHON $SOURCES $TARGETS',
)

env.Alias('generated-sources', generateActionTypes)

env.Library(
    target='authentication_restriction',
    source=[
        'restriction_environment.cpp',
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/util/net/network',
    ],
)

# The Auth library should consist only of the shimmed API for Auth usage and the implementations of
# the data structures used in that API.  No actual Auth subsystem implementation should exist in
# this library.
env.Library(
    target='auth',
    source=[
        'authorization_manager.cpp',
        'authorization_session.cpp',
        'auth_decorations.cpp',
        'user_name.cpp',
        'role_name.cpp',
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/db/service_context',
    ],
)

env.Library(
    target='auth_op_observer',
    source=[
        "auth_op_observer.cpp",
    ],
    LIBDEPS_PRIVATE=[
        'auth',
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/db/catalog/collection_options',
        '$VARIANT_DIR/mongo/db/index/index_descriptor',
        '$VARIANT_DIR/mongo/db/op_observer',
        '$VARIANT_DIR/mongo/db/op_observer_util',
        '$VARIANT_DIR/mongo/db/s/sharding_api_d',
    ]
)

env.Library(
    target='user',
    source=[
        'user.cpp',
    ],
    LIBDEPS=[
        'authprivilege',
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/crypto/sha_block_${MONGO_CRYPTO}',
    ],
)

env.Library(
    target='auth_impl_internal_local',
    source=[
        'authz_manager_external_state_local.cpp',
    ],
    LIBDEPS=[
        'auth_impl_internal',
    ],
)

env.Library(
    target="role_graph",
    source=[
        'role_graph.cpp',
        'role_graph_builtin_roles.cpp',
    ],
    LIBDEPS=[
        'auth',
        'authprivilege',
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/bson/mutable/mutable_bson',
    ],
)

env.Library(
    target='user_document_parser',
    source=[
        'user_document_parser.cpp',
    ],
    LIBDEPS=[
        'auth',
        'user',
        'role_graph_update',
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/bson/mutable/mutable_bson',
        '$VARIANT_DIR/mongo/bson/util/bson_extract',
    ],
)

env.Library(
    target='role_graph_update',
    source=[
        'role_graph_update.cpp',
        env.Idlc("auth_options.idl")[0],
    ],
    LIBDEPS=[
        'auth',
        'role_graph',
        '$VARIANT_DIR/mongo/db/update/update_driver',
    ],
)

env.Library(
    target='auth_impl_internal',
    source=[
        'authorization_manager_impl.cpp',
        'authorization_session_impl.cpp',
        'authz_manager_external_state.cpp',
        'authz_session_external_state.cpp',
        'user_set.cpp',
        env.Idlc('authorization_manager_impl_parameters.idl')[0],
    ],
    LIBDEPS=[
        'address_restriction',
        'auth',
        'authentication_restriction',
        'authorization_manager_global',
        'authprivilege',
        'role_graph',
        'sasl_options',
        'user',
        'user_document_parser',
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/base/secure_allocator',
        '$VARIANT_DIR/mongo/bson/mutable/mutable_bson',
        '$VARIANT_DIR/mongo/bson/util/bson_extract',
        '$VARIANT_DIR/mongo/db/catalog/document_validation',
        '$VARIANT_DIR/mongo/db/common',
        '$VARIANT_DIR/mongo/db/global_settings',
        '$VARIANT_DIR/mongo/db/namespace_string',
        '$VARIANT_DIR/mongo/db/pipeline/lite_parsed_document_source',
        '$VARIANT_DIR/mongo/db/update/update_driver',
        '$VARIANT_DIR/mongo/util/icu',
        '$VARIANT_DIR/mongo/util/net/ssl_manager',
        '$VARIANT_DIR/mongo/util/net/ssl_types',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/idl/server_parameter',
    ],
)

env.Library(
    target='authprivilege',
    source=[
        'action_set.cpp',
        'action_type.cpp',
        'impersonation_session.cpp',
        'privilege.cpp',
        'privilege_parser.cpp',
        'resource_pattern.cpp',
        'user_management_commands_parser.cpp',
    ],
    LIBDEPS=[
        'auth',
        'address_restriction',
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/bson/mutable/mutable_bson',
        '$VARIANT_DIR/mongo/db/common',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/rpc/metadata_impersonated_user',
    ]
)

env.Library(
    target='authorization_manager_global',
    source=[
        'authorization_manager_global.cpp',
        env.Idlc('authorization_manager_global_parameters.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/idl/server_parameter',
    ],
    LIBDEPS=[
        'auth',
        '$VARIANT_DIR/mongo/db/server_options_core',
        '$VARIANT_DIR/mongo/db/service_context',
    ],
)

env.Library(
    target='security_key',
    source=[
        'security_key.cpp',
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
    ],
    LIBDEPS_PRIVATE=[
        'sasl_options',
        'security_file',
        'user',
        '$VARIANT_DIR/mongo/base/secure_allocator',
        '$VARIANT_DIR/mongo/client/authentication',
        '$VARIANT_DIR/mongo/crypto/sha_block_${MONGO_CRYPTO}',
        '$VARIANT_DIR/mongo/util/icu',
        '$VARIANT_DIR/mongo/util/md5',
    ],
)

env.Library(
    target='authservercommon',
    source=[
        'authz_session_external_state_server_common.cpp',
        'sasl_commands.cpp',
        env.Idlc('enable_localhost_auth_bypass_parameter.idl')[0],
    ],
    LIBDEPS=[
        'auth',
        'auth_impl_internal',
        'authorization_manager_global',
        'saslauth',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/client/sasl_client',
        '$VARIANT_DIR/mongo/db/audit',
        '$VARIANT_DIR/mongo/db/commands',
        '$VARIANT_DIR/mongo/db/commands/authentication_commands',
        '$VARIANT_DIR/mongo/db/commands/test_commands_enabled',
        'sasl_options_init',
    ],
)

yamlEnv = env.Clone()
yamlEnv.InjectThirdParty(libraries=['yaml'])
yamlEnv.Library(
    target='security_file',
    source=[
        'security_file.cpp',
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/third_party/shim_yaml',
    ],
)

env.Library(
    target='sasl_options',
    source=[
        'sasl_options.cpp',
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/idl/server_parameter',
    ],
)

env.Library(
  target='sasl_options_init',
  source=[
      'sasl_options_init.cpp',
       env.Idlc('sasl_options.idl')[0],
  ],
  LIBDEPS=[
    'sasl_options'
  ],
  LIBDEPS_PRIVATE=[
       '$VARIANT_DIR/mongo/util/options_parser/options_parser',
       '$VARIANT_DIR/mongo/util/net/network',
  ],
)

env.Library(
    target='saslauth',
    source=[
        'sasl_mechanism_registry.cpp',
        'sasl_plain_server_conversation.cpp',
        'sasl_scram_server_conversation.cpp',
    ],
    LIBDEPS=[
        'auth',
        'authprivilege',
        'sasl_options',
        'user',
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/base/secure_allocator',
        '$VARIANT_DIR/mongo/crypto/sha_block_${MONGO_CRYPTO}',
        '$VARIANT_DIR/mongo/db/commands/test_commands_enabled',
        '$VARIANT_DIR/mongo/util/icu',
        '$VARIANT_DIR/mongo/util/md5',
        '$VARIANT_DIR/mongo/util/net/network',
    ],
)

env.Library(
    target='authmongod',
    source=[
        'authz_manager_external_state_d.cpp',
        'authz_session_external_state_d.cpp',
    ],
    LIBDEPS=[
        'authservercommon',
        'auth_impl_internal_local',
        '$VARIANT_DIR/mongo/db/db_raii',
        '$VARIANT_DIR/mongo/db/dbdirectclient',
        '$VARIANT_DIR/mongo/db/dbhelpers',
        '$VARIANT_DIR/mongo/db/repl/repl_coordinator_interface',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/db/concurrency/lock_manager',
    ],
)

env.Library(
    target='authmongos',
    source=[
        'authz_manager_external_state_s.cpp',
        'authz_session_external_state_s.cpp',
        'user_cache_invalidator_job.cpp',
        env.Idlc('user_cache_invalidator_job_parameters.idl')[0],
        ],
    LIBDEPS=[
        'authservercommon',
        '$VARIANT_DIR/mongo/s/catalog/dist_lock_manager',
        '$VARIANT_DIR/mongo/s/coreshard',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/idl/server_parameter',
    ],
)

env.Library(
    target='authmocks',
    source=[
        'authz_manager_external_state_mock.cpp',
        'authz_session_external_state_mock.cpp',
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/db/matcher/expressions',
        '$VARIANT_DIR/mongo/db/service_context',
        '$VARIANT_DIR/mongo/db/update/update_driver',
        'auth',
        'auth_impl_internal',
        'auth_impl_internal_local',
    ],
)

env.Library(
    target='address_restriction',
    source=[
        'address_restriction.cpp',
        env.Idlc('address_restriction.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        'authentication_restriction',
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/util/net/network',
        '$VARIANT_DIR/mongo/idl/idl_parser',
    ],
)

env.CppUnitTest(
    target='db_auth_test',
    source=[
        'action_set_test.cpp',
        'address_restriction_test.cpp',
        'auth_op_observer_test.cpp',
        'authorization_manager_test.cpp',
        'authorization_session_for_test.cpp',
        'authorization_session_test.cpp',
        'privilege_parser_test.cpp',
        'restriction_test.cpp',
        'role_graph_test.cpp',
        'sasl_authentication_session_test.cpp',
        'sasl_mechanism_registry_test.cpp',
        'sasl_scram_test.cpp',
        'security_key_test.cpp',
        'user_document_parser_test.cpp',
        'user_set_test.cpp',
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/client/sasl_client',
        '$VARIANT_DIR/mongo/db/common',
        '$VARIANT_DIR/mongo/db/pipeline/pipeline',
        '$VARIANT_DIR/mongo/db/repl/oplog',
        '$VARIANT_DIR/mongo/db/repl/oplog_interface_local',
        '$VARIANT_DIR/mongo/db/repl/replmocks',
        '$VARIANT_DIR/mongo/db/service_context_d_test_fixture',
        '$VARIANT_DIR/mongo/db/service_context_test_fixture',
        '$VARIANT_DIR/mongo/transport/transport_layer_common',
        '$VARIANT_DIR/mongo/transport/transport_layer_mock',
        '$VARIANT_DIR/mongo/util/net/network',
        'address_restriction',
        'auth',
        'auth_impl_internal',
        'auth_op_observer',
        'authentication_restriction',
        'authmocks',
        'saslauth',
        'security_file',
        'security_key',
        'user',
    ],
)
