# -*- mode: python -*-
Import("env")
Import("wiredtiger")
Import("get_option")

env = env.Clone()

using_ubsan = False
sanitizer_list = get_option('sanitize')
if sanitizer_list:
    using_ubsan = 'undefined' in sanitizer_list.split(',')

env.Library(
    target='storage_wiredtiger_customization_hooks',
    source= [
        'wiredtiger_customization_hooks.cpp',
        'wiredtiger_extensions.cpp',
    ],
    LIBDEPS= ['$VARIANT_DIR/mongo/base',
              '$VARIANT_DIR/mongo/db/service_context'],
    PROGDEPS_DEPENDENTS=[
        '$VARIANT_DIR/mongo/mongod',
        '$VARIANT_DIR/mongo/mongos',
    ],
)

if wiredtiger:
    wtEnv = env.Clone()
    wtEnv.InjectThirdParty(libraries=['wiredtiger'])
    wtEnv.InjectThirdParty(libraries=['zlib'])
    wtEnv.InjectThirdParty(libraries=['valgrind'])

    # This is the smallest possible set of files that wraps WT
    wtEnv.Library(
        target='storage_wiredtiger_core',
        source= [
            'wiredtiger_begin_transaction_block.cpp',
            'wiredtiger_cursor.cpp',
            'wiredtiger_global_options.cpp',
            'wiredtiger_index.cpp',
            'wiredtiger_kv_engine.cpp',
            'wiredtiger_oplog_manager.cpp',
            'wiredtiger_parameters.cpp',
            'wiredtiger_prepare_conflict.cpp',
            'wiredtiger_record_store.cpp',
            'wiredtiger_recovery_unit.cpp',
            'wiredtiger_session_cache.cpp',
            'wiredtiger_snapshot_manager.cpp',
            'wiredtiger_size_storer.cpp',
            'wiredtiger_util.cpp',
            env.Idlc('wiredtiger_parameters.idl')[0],
            ],
        LIBDEPS= [
            '$VARIANT_DIR/mongo/base',
            '$VARIANT_DIR/mongo/db/bson/dotted_path_support',
            '$VARIANT_DIR/mongo/db/commands/test_commands_enabled',
            '$VARIANT_DIR/mongo/db/catalog/collection',
            '$VARIANT_DIR/mongo/db/catalog/collection_options',
            '$VARIANT_DIR/mongo/db/concurrency/lock_manager',
            '$VARIANT_DIR/mongo/db/concurrency/write_conflict_exception',
            '$VARIANT_DIR/mongo/db/curop',
            '$VARIANT_DIR/mongo/db/index/index_descriptor',
            '$VARIANT_DIR/mongo/db/global_settings',
            '$VARIANT_DIR/mongo/db/namespace_string',
            '$VARIANT_DIR/mongo/db/prepare_conflict_tracker',
            '$VARIANT_DIR/mongo/db/repl/repl_coordinator_interface',
            '$VARIANT_DIR/mongo/db/repl/repl_settings',
            '$VARIANT_DIR/mongo/db/server_options_core',
            '$VARIANT_DIR/mongo/db/service_context',
            '$VARIANT_DIR/mongo/db/storage/index_entry_comparison',
            '$VARIANT_DIR/mongo/db/storage/journal_listener',
            '$VARIANT_DIR/mongo/db/storage/key_string',
            '$VARIANT_DIR/mongo/db/storage/kv/kv_prefix',
            '$VARIANT_DIR/mongo/db/storage/oplog_hack',
            '$VARIANT_DIR/mongo/db/storage/recovery_unit_base',
            '$VARIANT_DIR/mongo/db/storage/storage_file_util',
            '$VARIANT_DIR/mongo/db/storage/storage_options',
            '$VARIANT_DIR/mongo/util/concurrency/ticketholder',
            '$VARIANT_DIR/mongo/util/elapsed_tracker',
            '$VARIANT_DIR/mongo/util/processinfo',
            '$VARIANT_DIR/third_party/shim_snappy',
            '$VARIANT_DIR/third_party/shim_wiredtiger',
            '$VARIANT_DIR/third_party/shim_zlib',
            'storage_wiredtiger_customization_hooks',
            ],
        LIBDEPS_PRIVATE= [
            '$VARIANT_DIR/mongo/db/snapshot_window_options',
            '$VARIANT_DIR/mongo/db/storage/storage_repair_observer',
            '$VARIANT_DIR/mongo/util/options_parser/options_parser',
            ],
        )

    wtEnv.Library(
        target='storage_wiredtiger',
        source=[
            'wiredtiger_init.cpp',
            'wiredtiger_options_init.cpp',
            'wiredtiger_record_store_mongod.cpp',
            'wiredtiger_server_status.cpp',
            env.Idlc('wiredtiger_global_options.idl')[0],
        ],
        LIBDEPS=[
            'storage_wiredtiger_core',
            'storage_wiredtiger_customization_hooks',
            '$VARIANT_DIR/mongo/db/db_raii',
            '$VARIANT_DIR/mongo/db/storage/storage_engine_impl',
            '$VARIANT_DIR/mongo/db/storage/storage_engine_lock_file',
            '$VARIANT_DIR/mongo/db/storage/storage_engine_metadata',
        ],
        LIBDEPS_PRIVATE=[
            '$VARIANT_DIR/mongo/db/catalog/database_holder',
            '$VARIANT_DIR/mongo/db/commands/server_status',
            '$VARIANT_DIR/mongo/db/concurrency/lock_manager',
            '$VARIANT_DIR/mongo/db/storage/storage_engine_common',
            '$VARIANT_DIR/mongo/util/options_parser/options_parser',
        ],
    )

    wtEnv.Library(
        target='storage_wiredtiger_mock',
        source=[
            'wiredtiger_record_store_mock.cpp',
            ],
        LIBDEPS=['storage_wiredtiger_core',
             ]
        )

    wtEnv.CppUnitTest(
        target='storage_wiredtiger_init_test',
        source=['wiredtiger_init_test.cpp',
                ],
        LIBDEPS=[
            '$VARIANT_DIR/mongo/db/auth/authmocks',
            '$VARIANT_DIR/mongo/db/service_context',
            '$VARIANT_DIR/mongo/db/service_context_d',
            '$VARIANT_DIR/mongo/db/service_context_test_fixture',
            '$VARIANT_DIR/mongo/db/storage/storage_engine_metadata',
            '$VARIANT_DIR/mongo/db/storage/storage_options',
            '$VARIANT_DIR/mongo/db/storage/wiredtiger/storage_wiredtiger',
            '$VARIANT_DIR/mongo/db/storage/wiredtiger/storage_wiredtiger_core',
            ],
        )

    wtEnv.CppUnitTest(
        target='storage_wiredtiger_recovery_unit_test',
        source=[
            'wiredtiger_recovery_unit_test.cpp',
        ],
        LIBDEPS=[
            'storage_wiredtiger_mock',
            '$VARIANT_DIR/mongo/db/storage/recovery_unit_test_harness',
            '$VARIANT_DIR/mongo/util/clock_source_mock',
        ],
        LIBDEPS_PRIVATE=[
            '$VARIANT_DIR/mongo/db/auth/authmocks',
            '$VARIANT_DIR/mongo/db/index/index_access_methods',
            '$VARIANT_DIR/mongo/db/repl/repl_coordinator_interface',
            '$VARIANT_DIR/mongo/db/repl/replmocks',
        ],
    )

    wtEnv.Library(
        target='additional_wiredtiger_record_store_tests',
        source=[
            'wiredtiger_record_store_test.cpp',
        ],
        LIBDEPS=[
            '$VARIANT_DIR/mongo/db/storage/durable_catalog_impl',
            '$VARIANT_DIR/mongo/db/storage/record_store_test_harness',
            '$VARIANT_DIR/mongo/util/clock_source_mock',
            'storage_wiredtiger_mock',
        ],
    )

    wtEnv.Library(
        target='additional_wiredtiger_index_tests',
        source=[
            'wiredtiger_index_test.cpp',
        ],
        LIBDEPS=[
            '$VARIANT_DIR/mongo/db/storage/durable_catalog_impl',
            '$VARIANT_DIR/mongo/db/storage/sorted_data_interface_test_harness',
            'storage_wiredtiger_mock',
        ],
    )

    # All of these tests fail to compile under undefined behavior
    # sanitizer due to unexpressed circular dependency edges. In particular
    # they all need a definition from the 'catalog'.
    if not using_ubsan:
        wtEnv.CppUnitTest(
            target='storage_wiredtiger_record_store_test',
            source=[
                'wiredtiger_standard_record_store_test.cpp',
            ],
            LIBDEPS=[
                'additional_wiredtiger_record_store_tests',
            ],
            LIBDEPS_PRIVATE=[
                '$VARIANT_DIR/mongo/db/auth/authmocks',
                '$VARIANT_DIR/mongo/db/repl/repl_coordinator_interface',
                '$VARIANT_DIR/mongo/db/repl/replmocks',
            ],
        )

        wtEnv.CppUnitTest(
            target='storage_wiredtiger_prefixed_record_store_test',
            source=[
                'wiredtiger_prefixed_record_store_test.cpp',
            ],
            LIBDEPS=[
                'additional_wiredtiger_record_store_tests',
            ],
            LIBDEPS_PRIVATE=[
                '$VARIANT_DIR/mongo/db/auth/authmocks',
                '$VARIANT_DIR/mongo/db/repl/repl_coordinator_interface',
                '$VARIANT_DIR/mongo/db/repl/replmocks',
            ],
        )

        wtEnv.CppUnitTest(
            target='storage_wiredtiger_index_test',
            source=[
                'wiredtiger_standard_index_test.cpp',
            ],
            LIBDEPS=[
                'additional_wiredtiger_index_tests',
            ],
            LIBDEPS_PRIVATE=[
                '$VARIANT_DIR/mongo/db/auth/authmocks',
            ]
        )

        wtEnv.CppUnitTest(
            target='storage_wiredtiger_prefixed_index_test',
            source=[
                'wiredtiger_prefixed_index_test.cpp',
            ],
            LIBDEPS=[
                'additional_wiredtiger_index_tests',
            ],
            LIBDEPS_PRIVATE=[
                '$VARIANT_DIR/mongo/db/auth/authmocks',
            ]
        )

        wtEnv.CppUnitTest(
            target='storage_wiredtiger_kv_engine_test',
            source=[
                'wiredtiger_kv_engine_test.cpp',
            ],
            LIBDEPS=[
                '$VARIANT_DIR/mongo/db/auth/authmocks',
                '$VARIANT_DIR/mongo/db/storage/kv/kv_engine_test_harness',
                'storage_wiredtiger_mock',
            ],
            LIBDEPS_PRIVATE=[
                '$VARIANT_DIR/mongo/db/repl/repl_coordinator_interface',
                '$VARIANT_DIR/mongo/db/repl/replmocks',
                '$VARIANT_DIR/mongo/db/service_context_test_fixture',
            ],
        )

        wtEnv.CppUnitTest(
            target='storage_wiredtiger_util_test',
            source=['wiredtiger_util_test.cpp',
            ],
            LIBDEPS=[
                '$VARIANT_DIR/mongo/db/service_context',
                '$VARIANT_DIR/mongo/db/storage/durable_catalog_impl',
                'storage_wiredtiger_mock',
            ],
        )

        wtEnv.CppUnitTest(
            target='storage_wiredtiger_session_cache_test',
            source=['wiredtiger_session_cache_test.cpp',
            ],
            LIBDEPS=[
                'storage_wiredtiger_mock',
            ],
        )

        wtEnv.Benchmark(
            target='storage_wiredtiger_begin_transaction_block_bm',
            source='wiredtiger_begin_transaction_block_bm.cpp',
            LIBDEPS=[
                '$VARIANT_DIR/mongo/db/service_context',
                '$VARIANT_DIR/mongo/db/storage/durable_catalog_impl',
                '$VARIANT_DIR/mongo/unittest/unittest',
                '$VARIANT_DIR/mongo/util/clock_source_mock',
                'storage_wiredtiger_mock',
            ],
       )
