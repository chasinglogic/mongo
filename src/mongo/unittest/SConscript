# -*- mode: python; -*-

Import("env")

env = env.Clone()

env.Library(
    target="unittest",
    source=[
        'barrier.cpp',
        'bson_test_util.cpp',
        'death_test.cpp',
        'temp_dir.cpp',
        'unittest_helpers.cpp',
        'unittest.cpp',
        env.Idlc('unittest.idl')[0],
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/db/server_options_core',
        '$VARIANT_DIR/mongo/util/options_parser/options_parser',
    ],
)

env.Library(
    target="unittest_main",
    source=[
        'unittest_main.cpp',
        env.Idlc('unittest_options.idl')[0],
    ],
    LIBDEPS=[
        'unittest',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/util/options_parser/options_parser',
    ],
)

env.Library(
    target="integration_test_main",
    source=[
        'integration_test_main.cpp',
        env.Idlc('integration_test_main.idl')[0],
    ],
    LIBDEPS=[
        'unittest',
        '$VARIANT_DIR/mongo/base',
    ],
    LIBDEPS_PRIVATE=[
        '$VARIANT_DIR/mongo/client/connection_string',
        '$VARIANT_DIR/mongo/db/service_context',
        '$VARIANT_DIR/mongo/util/options_parser/options_parser',
        '$VARIANT_DIR/mongo/util/options_parser/options_parser_init',
    ],
)

bmEnv = env.Clone()
bmEnv.InjectThirdParty(libraries=['benchmark'])
bmEnv.Library(
    target='benchmark_main',
    source=[
        'benchmark_main.cpp'
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/base',
        '$VARIANT_DIR/mongo/db/service_context',
        '$VARIANT_DIR/third_party/shim_benchmark',
    ],
)

env.Library(
    target='task_executor_proxy',
    source=[
        'task_executor_proxy.cpp',
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/executor/task_executor_interface',
    ],
)

env.CppUnitTest(
    target='unittest_test',
    source=[
        'unittest_test.cpp',
        'fixture_test.cpp',
        'temp_dir_test.cpp',
    ],
)

env.Benchmark(
    target='system_resource_canary_bm',
    source=[
        'system_resource_canary_bm.cpp'
    ],
    LIBDEPS=[
        '$VARIANT_DIR/mongo/util/processinfo',
    ],
)
